import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    `maven-publish`
    java
    id("org.jetbrains.kotlin.jvm") version "1.8.22" apply false
    id("com.github.johnrengelman.shadow") version "7.1.2" apply false
}

// 当前 fork HEAD 的短哈希（7 位），用于拼出发布版本号 6.3.0-<短哈希>，与上游「每个提交一版」的命名保持一致；
// 取不到（非 git 环境等）时退化为 unknown，不阻塞构建。
val gitShortHash: String = runCatching {
    ProcessBuilder("git", "rev-parse", "--short=7", "HEAD")
        .directory(rootDir).start()
        .inputStream.bufferedReader().use { it.readText() }.trim()
}.getOrNull()?.takeIf { it.isNotEmpty() } ?: "unknown"

subprojects {
    apply(plugin = "java-library")
    apply(plugin = "org.jetbrains.kotlin.jvm")
    apply(plugin = "com.github.johnrengelman.shadow")
    apply(plugin = "maven-publish")

    repositories {
        maven("https://jitpack.io")
        maven("https://libraries.minecraft.net")
        maven("https://repo1.maven.org/maven2")
        maven("https://maven.aliyun.com/repository/central")
        maven("https://repo.codemc.io/repository/nms/")
        maven("https://repo.tabooproject.org/repository/releases")
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly(kotlin("stdlib"))
        compileOnly("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
        compileOnly("com.google.guava:guava:21.0")
        compileOnly("com.google.code.gson:gson:2.8.7")
        compileOnly("org.apache.commons:commons-lang3:3.5")
        compileOnly("org.tabooproject.reflex:reflex:1.2.4")
        compileOnly("org.tabooproject.reflex:analyser:1.2.4")
        // 测试依赖
        testImplementation(kotlin("stdlib"))
        testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
        testImplementation("com.google.guava:guava:21.0")
        testImplementation("com.google.code.gson:gson:2.8.7")
        testImplementation("org.apache.commons:commons-lang3:3.5")
        testImplementation("org.tabooproject.reflex:reflex:1.2.4")
        testImplementation("org.tabooproject.reflex:analyser:1.2.4")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.1")
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    }

    java {
        withSourcesJar()
    }

    tasks.withType<Test> {
        useJUnitPlatform()
    }

    tasks.withType<ShadowJar> {
        archiveClassifier.set("")
        relocate("org.tabooproject", "taboolib.library")
    }

    tasks.build {
        dependsOn("shadowJar")
    }

    tasks.withType<JavaCompile> {
        options.encoding = "UTF-8"
        options.compilerArgs.addAll(listOf("-XDenableSunApiLintControl"))
    }

    tasks.withType<KotlinCompile> {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = listOf("-Xjvm-default=all")
        }
    }

    configure<JavaPluginConvention> {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

gradle.buildFinished {
    buildDir.deleteRecursively()
}

subprojects
    .filter { it.name != "module" && it.name != "platform" && it.name != "expansion" && !it.name.startsWith("impl") }
    .forEach { proj ->
        proj.publishing { applyToSub(proj) }
    }

fun PublishingExtension.applyToSub(subProject: Project) {
    repositories {
        maven("https://maven.wcpe.top/repository/maven-releases/") {
            credentials {
                username = project.findProperty("wcpeUsername").toString()
                password = project.findProperty("wcpePassword").toString()
            }
            authentication {
                create<BasicAuthentication>("basic")
            }
        }
//        maven("http://repo.aeoliancloud.com/repository/releases") {
//            isAllowInsecureProtocol = true
//            credentials {
//                username = project.findProperty("aeolianUsername").toString()
//                password = project.findProperty("aeolianPassword").toString()
//            }
//            authentication {
//                create<BasicAuthentication>("basic")
//            }
//        }
        mavenLocal()
    }
    publications {
        create<MavenPublication>("maven") {
            // 构件名
            artifactId = if (subProject.ext.has("publishId")) subProject.ext.get("publishId").toString() else subProject.name
            // 组
            groupId = "io.izzel.taboolib"
            // 版本号
            version = when {
                project.hasProperty("devLocal") -> "${project.version}-local-dev"
                project.hasProperty("dev") -> "${project.version}-dev"
                else -> "${project.version}-$gitShortHash"
            }
            // 构件
            artifact(subProject.tasks["kotlinSourcesJar"])
            artifact(subProject.tasks["shadowJar"])
            println("> Apply \"$groupId:$artifactId:$version\"")
        }
    }
}